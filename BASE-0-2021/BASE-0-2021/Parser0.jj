PARSER_BEGIN(Parser0)

import java.util.List;
import java.util.ArrayList;

/** ID lister. */
public class Parser0 {
    public static void main(String args[]) {
    }
}



PARSER_END(Parser0)

SKIP :
{
  " "
| "\t"
| "\r"
| "\n"
}

TOKEN :
{
 < Num: (["0"-"9"]) + >
  |
  < PLUS : "+" >
  |
  < MINUS : "-">
  |
  < MUL : "*">
  |
  < DIV : "/">
  |
  < EQUALS : "=">
  |
  < DEREF : "!">
  |
  < TYPE : ":">
  |
  < ASS : ":=">
  |
  < SEQ : ";">
  |
  < NEQ : "~=">
  |
  < BEQ : "==">
  |
  < GT : ">">
  |
  < GTE : ">=">
  |
  < LT : "<">
  |
  < LTE : "<=">
  |
  < AND : "&&">
  |
  < OR : "||">
  |
  < NOPE : "~">
  |
  < LPAR : "(" >
  |
  < RPAR : ")" >
  |
  < DEF: "def" >
  |
  < IN : "in">
  |
  < END : "end">
  |
  < NEW : "new">
  |
  < IF : "if">
  |
  < THEN : "then">
  |
  < ELSE : "else">
  |
  < WHILE : "while">
  |
  < DO : "do">
  |
  < PRINTLN : "println">
  |
  < PRINT : "print">
  |
  < REF : "ref" >
  |
  < INT : "int" >
  |
  < BOOL : "bool" >
  |
  < TRUE : "true">
  |
  < FALSE : "false">
  |
  < DSEMI : ";;" >
  |
  < Id: ["a"-"z","A"-"Z"] ( ["a"-"z","A"-"Z","0"-"9"] )* >
}

ASTNode Start():
{
ASTNode t1;
}
{ (
    t1 = ExpSeq() <DSEMI>
    | < EOF > { t1 = null; }
)
	{ return t1; }
}

ASTNode ExpSeq():
{ ASTNode t1, t2; }
{
   (
     t1 = ExpComp() (<SEQ> t2=ExpComp() {t1 = new ASTSeq(t1, t2);})*
   )
   { return t1;}
}

ASTNode ExpComp():
{
  Token tok;
  ASTNode t1, t2;
}
{
  t1 = Exp() ( (tok = <BEQ> | tok = <NEQ> | tok = <ASS> | tok = <GT> | tok = < GTE > | tok = < LT >
   | tok = < LTE >| tok = < AND >| tok=< OR >) t2 = Exp()
  				{
                    if(tok.kind == ASS)
                        t1 = new ASTAssign(t1, t2);
                    else if(tok.kind == AND || tok.kind == OR)
                        t1 = new ASTCompareBool(t1, t2, tok.image);
                    else
                        t1 = new ASTCompare(t1, t2, tok.image);
  				}
  		)?
  {return t1;}
}


ASTNode Exp() :
{
ASTNode t1,t2;
Token tok;
}
{
     t1 = Term() ( ( tok=<PLUS> | tok=<MINUS>) t2=Term()

                 { if (tok.kind == PLUS)
                        t1 = new ASTAdd(t1,t2);
                   else
                        t1 = new ASTSub(t1,t2);
                 }

                 )*

    
     { return t1; }

}

ASTNode Term() :
{
ASTNode t1,t2;
Token token;
}
{
     t1=Fact() (( token=<MUL> | token =<DIV> ) t2=Fact()
      {if(token.kind == MUL)
        t1 = new ASTMul(t1,t2);
        else t1 = new ASTDiv(t1,t2);
      }
      ) *

     { return t1; }
	
}

IType Type() :
{IType t;}
{
( < BOOL > { t = new TypeBool(); }
| < INT > { t = new TypeInt(); }
| < REF > { t = new TypeRef(Type()); }
)
{return t; }
}

ASTNode Fact() :
{ ASTNode t1, t2, t3 = null;
 Token tok;
 List<Bind> binds = new ArrayList<Bind>();
 IType type = null;
 String id; }
{
   ( tok=<Num> { t1 = new ASTNum(Integer.parseInt(tok.image)); }
   | (tok=<TRUE> | tok =<FALSE>) { t1 = new ASTBool(tok.image.equals("true"));}
   | < NEW > {t1 = new ASTRef(Fact());}
   | <DEREF> {t1 = new ASTDeref(Fact());}
   | <LPAR> t1=ExpSeq() <RPAR>
   | <MINUS> {t1 = new ASTMinus(Fact());}
   | < NOPE > { t1 = new ASTNot(ExpSeq()); }
   | < DEF > (tok = <Id> { id = tok.image;} (<TYPE> type = Type())? <EQUALS> {t2 = ExpSeq(); binds.add(new Bind(id, t2, type)); })+ <IN> { t1 = new ASTDef(binds, ExpSeq());} <END>
   | tok = <Id> { t1 = new ASTId(tok.image);}
   | <IF> t1 = ExpSeq() <THEN> t2 = ExpSeq() (<ELSE> t3 = ExpSeq())? <END> {t1 = new ASTIf(t1, t2, t3);}
   | <WHILE> t1 = ExpSeq() <DO> t2 = ExpSeq() <END> {t1 = new ASTWhile(t1, t2);}
   | <PRINT> {t1= new ASTPrintln(Fact(), false); }
   | <PRINTLN>  {t1 = new ASTPrintln(Fact(), true); }
   )   
  { return t1; }
}
